let checkVariation(test: Test, re: CompiledRegex): Void | Bubble {
  let check(string: String, expected: String): Void {
    assert(re.find(string)["full"].value == expected);
  }
  check("all", expected = "a");
  check("beautify", expected = "b");
  check("can", expected = "c");
  check("demonstrate", expected = "d");
  check("regex", expected = "e");
  check("functions", expected = "f");
}

test("code set") { (test);;
  checkVariation(test, compile("[abcdef]"));
}

test("or") { (test);;
  checkVariation(test, compile("a|b|c|d|e|f"));
}

test("code range") { (test);;
  checkVariation(test, compile("[a-f]"));
}

test("sub") { (test);;
  let regex = compileWith(
    "(?$a-through-f)",
    [
      "a-through-f",
    ],
    [
      subWith(
        "(?$char-a)|(?$char-b)|(?$char-c)|(?$char-d)|(?$char-e)|(?$char-f)",
        [
          "char-a",
          "char-b",
          "char-c",
          "char-d",
          "char-e",
          "char-f",
        ],
        [
          // Make one explicit simple string just for demo.
          new CodePoints("a"),
          subWith(
            "(?$char-b)",
            [ "char-b" ],
            [ sub("b") ]
          ),
          sub("(?cee=c)"),
          sub("(d)"),
          sub("(e|e)"),
          sub("[f-f]"),
        ]
      ),
    ]
  );
  checkVariation(test, regex);
}
